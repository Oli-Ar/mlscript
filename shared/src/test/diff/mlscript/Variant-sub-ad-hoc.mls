class None
class Some
//│ Defined class None
//│ Defined class Some

def Some v = Some{} with {v}
def None = None{}
//│ Some: 'a -> (Some & {v: 'a})
//│     = [Function: Some1]
//│ None: None
//│     = None {}

def flatMap = fun f -> fun opt ->
  case opt of { Some -> f opt.v | _ -> opt }
//│ flatMap: ((forall 'a, 'v. ('v
//│   where
//│     'a <: {v: 'v})) -> 'b) -> (Some & 'a | 'b & ~some) -> 'b
//│        = [Function: flatMap]

f = fun x -> Some x
res = flatMap f (Some 1)
res = flatMap f None
//│ f: 'a -> (Some & {v: 'a})
//│  = [Function: f]
//│ res: Some & {v: forall 'a, 'v. ('v
//│   where
//│     'a <: {v: 'v})}
//│    = Some { v: 1 }
//│ res: None | Some & {v: forall 'a, 'v. ('v
//│   where
//│     'a <: {v: 'v})}
//│    = None {}

class Lit
class Neg
class Var
class Plus
//│ Defined class Lit
//│ Defined class Neg
//│ Defined class Var
//│ Defined class Plus

// * Note: had to increase fuel and depth limit for this case...
// * why does it seem to take more steps now?
:stats
rec def evalOpt = fun x -> case x of {
  | Lit ->
      Some x.v
  | Neg ->
      flatMap (fun s -> Some (0 - s)) (evalOpt x.sub)
  | Var ->
      None with {err = concat "free var: " x.nme}
  | Plus ->
      flatMap (fun l -> flatMap (fun r ->
        Some (l + r)
      ) (evalOpt x.rhs)) (evalOpt x.lhs)
  }
//│ evalOpt: 'lhs -> (None & {err: string} | Some & {v: int})
//│   where
//│     'lhs <: Lit & {v: int} | Neg & {sub: 'lhs} | Plus & {lhs: 'lhs, rhs: 'lhs} | Var & {nme: string}
//│        = [Function: evalOpt]
//│ constrain calls  : 1009
//│ annoying  calls  : 231
//│ subtyping calls  : 1391

:stats
evalOpt (Plus{} with {lhs = Lit{} with {v=2}; rhs = Lit{} with {v=2}})
//│ res: None & {err: string} | Some & {v: int}
//│    = Some { v: 4 }
//│ constrain calls  : 105
//│ annoying  calls  : 49
//│ subtyping calls  : 351

